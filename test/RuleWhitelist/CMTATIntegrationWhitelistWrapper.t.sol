// SPDX-License-Identifier: MPL-2.0
pragma solidity ^0.8.20;

import {Test} from "forge-std/Test.sol";
import {HelperContract} from "../HelperContract.sol";
import {CMTATDeployment} from "RuleEngine/../test/utils/CMTATDeployment.sol";
import {RuleWhitelist} from "src/rules/validation/deployment/RuleWhitelist.sol";
import {RuleWhitelistWrapper} from "src/rules/validation/deployment/RuleWhitelistWrapper.sol";
import {RuleEngine} from "RuleEngine/deployment/RuleEngine.sol";
import {AccessControlModuleStandalone} from "../../src/modules/AccessControlModuleStandalone.sol";
/**
 * @title Integration test with the CMTAT
 */

contract CMTATIntegrationWhitelistWrapper is Test, HelperContract {
    uint256 constant ADDRESS1_BALANCE_INIT = 31;
    uint256 constant ADDRESS2_BALANCE_INIT = 32;
    uint256 constant ADDRESS3_BALANCE_INIT = 33;

    RuleWhitelist ruleWhitelist2;
    RuleWhitelist ruleWhitelist3;
    RuleWhitelistWrapper ruleWhitelistWrapper;

    // Arrange
    function setUp() public {
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist = new RuleWhitelist(DEFAULT_ADMIN_ADDRESS, ZERO_ADDRESS, true, false);
        ruleWhitelist2 = new RuleWhitelist(DEFAULT_ADMIN_ADDRESS, ZERO_ADDRESS, true, false);
        ruleWhitelist3 = new RuleWhitelist(DEFAULT_ADMIN_ADDRESS, ZERO_ADDRESS, true, false);
        ruleWhitelistWrapper = new RuleWhitelistWrapper(DEFAULT_ADMIN_ADDRESS, ZERO_ADDRESS, true);
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelistWrapper.addRule(ruleWhitelist);
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelistWrapper.addRule(ruleWhitelist2);
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelistWrapper.addRule(ruleWhitelist3);
        // global arrange
        cmtatDeployment = new CMTATDeployment();
        cmtatContract = cmtatDeployment.cmtat();
        // specific arrange
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleEngineMock = new RuleEngine(DEFAULT_ADMIN_ADDRESS, ZERO_ADDRESS, address(cmtatContract));
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleEngineMock.addRule(ruleWhitelistWrapper);
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        cmtatContract.mint(ADDRESS1, ADDRESS1_BALANCE_INIT);
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        cmtatContract.mint(ADDRESS2, ADDRESS2_BALANCE_INIT);
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        cmtatContract.mint(ADDRESS3, ADDRESS3_BALANCE_INIT);
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        // We set the Rule Engine
        cmtatContract.setRuleEngine(ruleEngineMock);
    }

    /**
     * Deployment ******
     */
    function testCannotDeployContractIfAdminAddressIsZero() public {
        vm.prank(WHITELIST_OPERATOR_ADDRESS);
        vm.expectRevert(AccessControlModuleStandalone.AccessControlModuleStandalone_AddressZeroNotAllowed.selector);
        ruleWhitelistWrapper = new RuleWhitelistWrapper(ZERO_ADDRESS, ZERO_ADDRESS, true);
    }

    /**
     * Transfer ******
     */
    function testCannotTransferWithoutAddressWhitelisted() public {
        // Arrange
        vm.prank(ADDRESS1);
        vm.expectRevert(
            abi.encodeWithSelector(
                RuleWhitelist_InvalidTransfer.selector,
                address(ruleWhitelistWrapper),
                ADDRESS1,
                ADDRESS2,
                21,
                CODE_ADDRESS_FROM_NOT_WHITELISTED
            )
        );
        // Act
        // forge-lint: disable-next-line(erc20-unchecked-transfer)
        cmtatContract.transfer(ADDRESS2, 21);
    }

    function testCannotTransferWithoutFromAddressWhitelisted() public {
        // Arrange
        uint256 amount = 21;
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist.addAddress(ADDRESS2);

        vm.prank(ADDRESS1);
        vm.expectRevert(
            abi.encodeWithSelector(
                RuleWhitelist_InvalidTransfer.selector,
                address(ruleWhitelistWrapper),
                ADDRESS1,
                ADDRESS2,
                amount,
                CODE_ADDRESS_FROM_NOT_WHITELISTED
            )
        );
        // Act
        // forge-lint: disable-next-line(erc20-unchecked-transfer)
        cmtatContract.transfer(ADDRESS2, amount);
    }

    function testCannotTransferWithoutToAddressWhitelisted() public {
        // Arrange
        uint256 amount = 21;
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist.addAddress(ADDRESS1);

        vm.prank(ADDRESS1);
        vm.expectRevert(
            abi.encodeWithSelector(
                RuleWhitelist_InvalidTransfer.selector,
                address(ruleWhitelistWrapper),
                ADDRESS1,
                ADDRESS2,
                amount,
                CODE_ADDRESS_TO_NOT_WHITELISTED
            )
        );
        // Act
        // forge-lint: disable-next-line(erc20-unchecked-transfer)
        cmtatContract.transfer(ADDRESS2, amount);
    }

    function testCanMakeATransferIfWhitelistedInSeveralDifferentList() public {
        // Arrange
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist.addAddress(ADDRESS1);
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist3.addAddress(ADDRESS1);

        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist2.addAddress(ADDRESS2);
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist3.addAddress(ADDRESS2);

        // Act
        vm.prank(ADDRESS1);
        assertTrue(cmtatContract.transfer(ADDRESS2, 11));

        // Assert
        resUint256 = cmtatContract.balanceOf(ADDRESS1);
        assertEq(resUint256, 20);
        resUint256 = cmtatContract.balanceOf(ADDRESS2);
        assertEq(resUint256, 43);
        resUint256 = cmtatContract.balanceOf(ADDRESS3);
        assertEq(resUint256, 33);
    }

    function testCanMakeATransferIfWhitelistedInDifferentList() public {
        // Arrange
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist.addAddress(ADDRESS1);

        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist2.addAddress(ADDRESS2);

        // Act
        vm.prank(ADDRESS1);
        assertTrue(cmtatContract.transfer(ADDRESS2, 11));

        // Assert
        resUint256 = cmtatContract.balanceOf(ADDRESS1);
        assertEq(resUint256, 20);
        resUint256 = cmtatContract.balanceOf(ADDRESS2);
        assertEq(resUint256, 43);
        resUint256 = cmtatContract.balanceOf(ADDRESS3);
        assertEq(resUint256, 33);
    }

    function testCanMakeATransfer() public {
        // Arrange
        address[] memory whitelist = new address[](2);
        whitelist[0] = ADDRESS1;
        whitelist[1] = ADDRESS2;
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        (bool success,) = address(ruleWhitelist).call(abi.encodeWithSignature("addAddresses(address[])", whitelist));
        require(success);

        // Act
        vm.prank(ADDRESS1);
        assertTrue(cmtatContract.transfer(ADDRESS2, 11));

        // Assert
        resUint256 = cmtatContract.balanceOf(ADDRESS1);
        assertEq(resUint256, 20);
        resUint256 = cmtatContract.balanceOf(ADDRESS2);
        assertEq(resUint256, 43);
        resUint256 = cmtatContract.balanceOf(ADDRESS3);
        assertEq(resUint256, 33);
    }

    /**
     * detectTransferRestriction & messageForTransferRestriction ******
     */
    function testDetectAndMessageWithFromNotWhitelisted() public {
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist.addAddress(ADDRESS2);
        resBool = ruleWhitelist.isAddressListed(ADDRESS2);
        // Assert
        assertEq(resBool, true);
        uint8 res1 = cmtatContract.detectTransferRestriction(ADDRESS1, ADDRESS2, 11);
        // Assert
        assertEq(res1, CODE_ADDRESS_FROM_NOT_WHITELISTED);
        string memory message1 = cmtatContract.messageForTransferRestriction(res1);
        // Assert
        assertEq(message1, TEXT_ADDRESS_FROM_NOT_WHITELISTED);
    }

    function testDetectAndMessageWithToNotWhitelisted() public {
        // Arrange
        // We add the sender to the whitelist
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist.addAddress(ADDRESS1);
        // Arrange - Assert
        resBool = ruleWhitelist.isAddressListed(ADDRESS1);
        assertEq(resBool, true);
        // Act
        uint8 res1 = cmtatContract.detectTransferRestriction(ADDRESS1, ADDRESS2, 11);
        // Assert
        assertEq(res1, CODE_ADDRESS_TO_NOT_WHITELISTED);
        // Act
        string memory message1 = cmtatContract.messageForTransferRestriction(res1);
        // Assert
        assertEq(message1, TEXT_ADDRESS_TO_NOT_WHITELISTED);
    }

    function testDetectAndMessageWithFromAndToNotWhitelisted() public view {
        // Act
        uint8 res1 = cmtatContract.detectTransferRestriction(ADDRESS1, ADDRESS2, 11);

        // Assert
        assertEq(res1, CODE_ADDRESS_FROM_NOT_WHITELISTED);
        // Act
        string memory message1 = cmtatContract.messageForTransferRestriction(res1);

        // Assert
        assertEq(message1, TEXT_ADDRESS_FROM_NOT_WHITELISTED);
    }

    function testCanReturnUnknownTextMessage() public view {
        // Act
        string memory message1 = cmtatContract.messageForTransferRestriction(200);

        // Assert
        assertEq(message1, TEXT_CODE_NOT_FOUND);
    }

    function testDetectAndMessageWithAValidTransfer() public {
        // Arrange
        // We add the sender and the recipient to the whitelist.
        address[] memory whitelist = new address[](2);
        whitelist[0] = ADDRESS1;
        whitelist[1] = ADDRESS2;
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        (bool success,) = address(ruleWhitelist).call(abi.encodeWithSignature("addAddresses(address[])", whitelist));
        require(success);
        // Act
        uint8 res1 = cmtatContract.detectTransferRestriction(ADDRESS1, ADDRESS2, 11);
        // Assert
        assertEq(res1, TRANSFER_OK);
        // Act
        string memory message1 = cmtatContract.messageForTransferRestriction(res1);
        // Assert
        assertEq(message1, TEXT_TRANSFER_OK);
    }

    function testCanMint() public {
        // Arrange
        // Add address zero to the whitelist
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist.addAddress(ZERO_ADDRESS);
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        ruleWhitelist.addAddress(ADDRESS1);
        // Arrange - Assert
        resBool = ruleWhitelist.isAddressListed(ZERO_ADDRESS);
        assertEq(resBool, true);

        // Act
        vm.prank(DEFAULT_ADMIN_ADDRESS);
        cmtatContract.mint(ADDRESS1, 11);

        // Assert
        resUint256 = cmtatContract.balanceOf(ADDRESS1);
        assertEq(resUint256, ADDRESS1_BALANCE_INIT + 11);
    }
}
